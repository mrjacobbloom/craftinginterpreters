// Lox doesn't have a collection type. Let's make one!

class LinkedList {
  init() {
    this.head = nil;
    this.length = 0;

    class LLNode {
      init(value, next, index) {
        this.value = value;
        this.next = next;
        this.index = index;
      }
    }
    this.LLNode = LLNode;
  }

  add(value) {
    this.head = this.LLNode(value, this.head, this.length);
    this.length = this.length + 1;
  }

  _getNodeAt(goalIndex) {
    if (this.head == nil) return nil;
    if (goalIndex < 0 or goalIndex >= this.length) return nil;
    var current = this.head;
    // Since we can't delete/move/insert nodes, this should be robust enough. I think.
    while(current.index != goalIndex) current = current.next;
    return current;
  }

  getAt(goalIndex) {
     var node = this._getNodeAt(goalIndex);
     if (node == nil) return nil;
     return node.value;
  }

  setAt(goalIndex, newValue) {
     var node = this._getNodeAt(goalIndex);
     if (node != nil) node.value = newValue;
  }

  printLL() {
    if (this.head) {
      var out = "[ ";
      for(var i = 0; i < this.length; i = i + 1) {
        out = out + toString(i) + ": " + toString(this._getNodeAt(i).value);
        if (i != this.length - 1) out = out + ", ";
      }
      print out + " ]";
    } else {
      print "[ empty ]";
    }
  }
}

print "creating LL";
var ll = LinkedList();

print "length -> " + toString(ll.length);

print "add('foo') at 0"; ll.add("foo");
print "add('bar') at 1"; ll.add("bar");
print "add('baz') at 2"; ll.add("baz");

print "setAt(1, 'qux')"; ll.setAt(1, "qux");

print "length -> " + toString(ll.length);

for(var i = 0; i < ll.length; i = i + 1) print "getAt(" + toString(i) + ") -> " + toString(ll.getAt(i));

ll.printLL();